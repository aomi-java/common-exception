apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            //如果是war包填写components.web，如果是jar包填写components.java
            from components.java
            artifact sourcesJar
            pom {
                name = 'Exception'
                description = 'Aomi Tech Common Exception'
                url = 'https://github.com/aomi-java/common-exception'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Nicholas'
                        name = 'Nicholas Tian'
                        email = 'sean.snow@live.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/aomi-java/common-exception.git'
                    developerConnection = 'scm:git:https://github.com/aomi-java/common-exception.git'
                    url = 'https://github.com/aomi-java/common-exception'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            //认证用户和密码
            credentials {
                username System.getenv('OSSRH_USERNAME')
                password System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
